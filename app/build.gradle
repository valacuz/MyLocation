apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tool_version

    defaultConfig {
        applicationId "com.example.valacuz.mylocations"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled true
    }
    sourceSets {
        main.java.srcDirs = ['src/main/kotlin']
        test.java.srcDirs = ['src/test/kotlin']
        androidTest.java.srcDirs = ['src/androidTest/kotlin']
        mock.java.srcDirs = ['src/mock/kotlin']
        production.java.srcDirs = ['src/prod/kotlin']
    }
    flavorDimensions "default"
    productFlavors {
        mock {
            dimension "default"
            applicationIdSuffix = ".mock"
        }
        production {
            dimension "default"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release' && variant.flavors.get(0).name == 'mock') {
            variant.ignore = true
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'failed', 'skipped', 'standardOut', 'standardError'
        }
    }
}

kapt {
    generateStubs = true
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Android support libraries
    implementation "com.android.support:appcompat-v7:$android_support_version"
    implementation "com.android.support:design:$android_support_version"
    implementation "com.android.support:recyclerview-v7:$android_support_version"
    implementation "com.android.support:support-v4:$android_support_version"
    implementation "com.android.support.constraint:constraint-layout:$android_constraint_version"

    // Google play services
    implementation "com.google.android.gms:play-services-maps:$google_play_version"
    implementation "com.google.android.gms:play-services-location:$google_play_version"

    // GSON
    implementation "com.google.code.gson:gson:$gson_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    // RxJava2
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    // Android data binding
    kapt "com.android.databinding:compiler:$gradle_version"

    // Espresso
    implementation "com.android.support.test.espresso:espresso-idling-resource:$espresso_version"

    // Dagger2
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // Room persistence
    kapt "android.arch.persistence.room:compiler:$android_component_version"
    implementation "android.arch.persistence.room:rxjava2:$android_component_version"
    implementation "android.arch.persistence.room:runtime:$android_component_version"

    // JUnit
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'junit:junit:4.12'

    // Room persistence for testing
    androidTestImplementation "android.arch.core:core-testing:$android_component_version"
    androidTestImplementation "android.arch.persistence.room:testing:$android_component_version"

    //
    androidTestImplementation "com.android.support.test:runner:$test_runner_version"
    androidTestImplementation "com.android.support.test:rules:$test_rule_version"

    // Espresso
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-idling-resource:$espresso_version"

    // Ui Automator
//    androidTestImplementation "com.android.support.test.uiautomator:uiautomator-v18:$ui_automator_version"
}
